#BrainMan: http://www.mobygames.com/game/brainman

BasicGame
    SpriteSet
        avatar > OrientedAvatar img=alien rotateInPlace=false
        gem > Immovable
            green > Immovable img=diamond
            red > Immovable img=powerpill
            blue > Immovable img=mana
        key > Passive img=key
        keym > Missile img=city
        exit > Immovable img=goal
        door > Immovable img=door
        boulder > Passive img=boulder 
    LevelMapping
        A > avatar
        K > key
        D > door
        E > exit
        g > green
        r > red
        b > blue 
        O > boulder
    InteractionSet
        keym key > transformTo stype=key
        keym wall > transformTo stype=key
        keym gem > transformTo stype=key
        keym boulder > transformTo stype=key
        
        avatar wall > stepBack
        avatar door > stepBack
        
        boulder avatar > bounceForward
        
        key avatar > transformTo stype=keym
        keym avatar > attractGaze
        avatar key > stepBack
        avatar keym > stepBack
        
        door keym > killSprite scoreChange=4
        keym door > killSprite
        
        green avatar > killSprite scoreChange=1
        blue avatar > killSprite scoreChange=2
        red avatar > killSprite scoreChange=5
        
        boulder wall > undoAll
        boulder key > undoAll
        boulder gem > undoAll
        boulder boulder > undoAll
        
        key wall > undoAll
        key gem > undoAll
        key key > undoAll
        
        exit avatar > killSprite scoreChange=10
        
    TerminationSet
        SpriteCounter stype=avatar limit=0 win=False
        SpriteCounter stype=exit limit=0 win=True